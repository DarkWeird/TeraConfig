apply plugin: 'maven-publish'

group = 'org.terasology.config'

task zipCodeMetrics(type: Zip) {
    description 'Creates a zip archive that contains all codemetric config files'

    from ('.') {
	    include 'checkstyle/'
	    include 'pmd/'
	    include 'findbugs/'
    }

    destinationDir = file(buildDir)
    archiveName = 'codemetrics.zip'
}

publishing {
    publications {
        codemetrics(MavenPublication) {
            artifact zipCodeMetrics

            artifactId 'codemetrics'
        }
    }

    repositories {
        maven {
            if (project.hasProperty('artifactoryUser') && project.hasProperty('artifactoryPass')) {
                credentials {
                    username artifactoryUser
                    password artifactoryPass
                }
            }        
            url "http://artifactory.terasology.org/artifactory/libs-release-local"
        }
    }
}
